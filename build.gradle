plugins {
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.sonarqube' version '2.7'
    id 'maven-publish'
    id 'java'
}

allprojects {
    group = 'com.co.leader.admin'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'

    sourceCompatibility = JavaVersion.VERSION_17

    dependencies {
        implementation 'ch.qos.logback:logback-classic'
        implementation 'ch.qos.logback:logback-core'
        implementation 'org.slf4j:slf4j-api'
        implementation 'org.springframework:spring-context'
        implementation 'org.springframework:spring-core'
        implementation 'org.springframework:spring-context-support'
        implementation 'joda-time:joda-time:2.10.10'
        implementation 'org.projectlombok:lombok:1.18.22'
        implementation 'org.mapstruct:mapstruct:1.4.2.Final'

        annotationProcessor 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
        }

        dependencies {
            dependencySet(group: "com.co.leader.commons", version: "${leader_commons_version}") {
                entry 'commons-util'
                entry 'commons-dto'
                entry 'commons-dto-jpa-support'
                entry 'commons-repository'
                entry 'commons-web'
                entry 'commons-security'
                entry 'commons-properties'
            }
        }
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("${github_packages_url}/leader-admin")
                credentials {
                    username = System.getenv("GH_PACKAGES_USERNAME")
                    password = System.getenv("GH_PACKAGES_TOKEN")
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    sonarqube {
        properties {
            property 'sonar.sources', 'src/main/java'
            property 'sonar.exclusions', '**/*Test*/**'
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

task version() {
    doLast {
        println project.version
    }
}